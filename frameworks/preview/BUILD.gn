# Copyright (c) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../device_status.gni")

config("devicestatus_drag_preview_config") {
  include_dirs = [
    "include",
    "${device_status_root_path}/utils/common/include",
  ]

  defines = device_status_default_defines
}

config("devicestatus_drag_preview_public_config") {
  include_dirs = [ "include" ]
}

template("msdp_devicestatus_drag_preview_shared_library") {
  forward_variables_from(invoker, "*")

  ohos_shared_library(target_name) {
    sources = [ "src/drag_preview.cpp" ]

    configs = [ ":devicestatus_drag_preview_config" ]

    public_configs = [ ":devicestatus_drag_preview_public_config" ]

    deps = [
      "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog_${platform}",
    ]

    subsystem_name = "${device_status_subsystem_name}"
    part_name = "${device_status_part_name}"
  }
}

platforms = [
  "mac",
  "windows",
  "linux",
]

foreach(item, platforms) {
  msdp_devicestatus_drag_preview_shared_library("devicestatus_drag_preview_" + item) {
    platform = item
  }
}
